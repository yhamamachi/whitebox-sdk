@echo off

IF NOT EXIST deploy (
  echo [ERROR]There is no deploy directory, please copy the deploy directory generated by the Whitebox build.
  exit /b 0
)

if "%1"=="" (
  echo [ERROR]You can select the OS to write to by specifying the option.
  call:Usage
  exit /b 0
)

if %1 == -1 (
  echo  OS type to be written  G4MH=Trampoline CR52=Trampoline
) else if %1 == -2 (
  echo  OS type to be written  G4MH=SafeG-Auto CR52=Trampoline
) else if %1 == -3 (
  echo  OS type to be written  G4MH=Trampoline CR52=Zephyr
) else if %1 == -4 (
  echo  OS type to be written  G4MH=SafeG-Auto CR52=Zephyr
) else (
  echo [ERROR]The option is incorrectly specified.
  call:Usage
  exit /b 0
)

copy /y deploy\*.srec

if %1 == -1 (
  copy deploy\g4mh_trampoline_deploy\g4mh.srec App_CDD_ICCOM_S4_Sample_G4MH.srec
  copy deploy\cr52_trampoline_deploy\cr52.srec App_CDD_ICCOM_S4_Sample_CR52.srec
) else if %1 == -2 (
  copy deploy\g4mh_safegauto_deploy\g4mh.srec App_CDD_ICCOM_S4_Sample_G4MH.srec
  copy deploy\cr52_trampoline_deploy\cr52.srec App_CDD_ICCOM_S4_Sample_CR52.srec
) else if %1 == -3 (
  copy deploy\g4mh_trampoline_deploy\g4mh.srec App_CDD_ICCOM_S4_Sample_G4MH.srec
  copy deploy\cr52_zephyr_deploy\cr52.srec App_CDD_ICCOM_S4_Sample_CR52.srec
) else if %1 == -4 (
  copy deploy\g4mh_safegauto_deploy\g4mh.srec App_CDD_ICCOM_S4_Sample_G4MH.srec
  copy deploy\cr52_zephyr_deploy\cr52.srec App_CDD_ICCOM_S4_Sample_CR52.srec
)

setlocal enabledelayedexpansion

set input_file=cert_header_sa9_bak.srec
set output_file=cert_header_sa9.srec
set search_text=S315EB23695000000000000010E2000000000000000031
set replace_text=S315EB23695000000000000004400000000000000000DF

IF NOT EXIST cert_header_sa9_bak.srec (
    copy cert_header_sa9.srec cert_header_sa9_bak.srec
)

(for /f "tokens=*" %%a in (%input_file%) do (
    set "line=%%a"
    set "line=!line:%search_text%=%replace_text%!"
    echo !line!
)) > %output_file%

IF NOT EXIST Flash_Bootloader_Spider.ttl (
    set input_file="Flash_Bootloader_S4SK.ttl"
    set output_file="Flash_Bootloader_Spider.ttl"
    set search_text=\"s4sk\"
    set replace_text=\"spider\"

    Powershell -Command "Get-Content !input_file! | ForEach-Object { $_ -replace !search_text!, !replace_text! } | Set-Content !output_file!"
)

endlocal
exit /b 0

:Usage
  echo Usage : setup_flashboot.bat [option]
  echo option:
  echo   -1: G4MH=Trampoline CR52=Trampoline
  echo   -2: G4MH=SafeG-Auto CR52=Trampoline
  echo   -3: G4MH=Trampoline CR52=Zephyr
  echo   -4: G4MH=SafeG-Auto CR52=Zephyr
  echo   -h: Show this usage
  exit /b 0
